buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.4.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'eclipse'
apply plugin: "jacoco"
apply plugin: "com.github.kt3k.coveralls"
apply plugin: 'com.jfrog.bintray'

archivesBaseName = 'smartcanvas-java-sdk'
sourceCompatibility = 1.7
version = '0.1.3'
group = 'com.smartcanvas'
jar {
	manifest {
		attributes 'Implementation-Title': 'Smart Canvas Java SDK Client', 'Implementation-Version': version
	}
}

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

dependencies {
	compile group: 'com.google.http-client', name: 'google-http-client', version: '1.20.0'
	compile 'org.bitbucket.b_c:jose4j:0.4.3'
	compile 'joda-time:joda-time:2.8.1'

	testCompile group: 'com.google.http-client', name: 'google-http-client-jackson2', version: '1.20.0'
	testCompile group: 'junit', name: 'junit', version: '4.+'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.5'
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

Properties properties = new Properties()
try {
	properties.load(project.rootProject.file('local.properties').newDataInputStream())
} catch(all) {
}

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    dryRun = false 
    publish = true    
    pkg {
        repo = 'maven'
        name = archivesBaseName
        vcsUrl = 'https://github.com/smartcanvas/java-sdk.git'
        issueTrackerUrl = 'https://github.com/smartcanvas/java-sdk/issues'
        labels = ['smartcanvas', 'api-client', 'java-sdk']
        userOrg = 'smartcanvas'
        licenses = ['MIT']
    }
}